import openflow.http (Client, Request, retry)
import std.int (Format)
import std.io (Buffer)
import std.json (Json)
import std.time (Duration)

# The time after which a request times out.
let TIMEOUT = 30

# The sensor states.
type pub inline Sensors {
  # A Map that maps sensor IDs to a boolean indicating if motion is detected.
  let pub @motion: Map[Int, Bool]
}

# An API/HTTP client for interacting with the Philips Hue bridge and motion
# sensors.
type pub Hue {
  let @client: Client
  let @user: String

  # The amount of time to wait after retrying a failed operation.
  let pub mut @retry_wait_time: Duration

  fn pub static new(client: Client, user: String) -> Hue {
    Hue(client: client, user: user, retry_wait_time: Duration.from_secs(5))
  }

  fn pub mut sensors -> Result[Sensors, String] {
    let request = Request.get('/api/${@user}/sensors')
    let timeout = Duration.from_secs(TIMEOUT)
    let resp = try retry(@retry_wait_time, fn {
      @client.timeout_after = timeout
      @client.execute(request)
    })
      .map_error(fn (e) { e.to_string })

    let root = match Json.parse(Buffer.new(resp.body)) {
      case Ok(Object(map)) -> map
      case _ -> throw 'Expected the response to be a JSON object'
    }

    let motion = Map.new

    for (k, v) in root.into_iter {
      let id = Int.parse(k, Format.Decimal).get
      let val = match v {
        case Object(map) -> map
        case _ -> next
      }

      let state = match val.remove('state') {
        case Ok(Object(map)) -> map
        case _ -> next
      }

      match val.get('type') {
        case Ok(String('ZLLPresence')) -> {
          match state.get('presence') {
            case Ok(Bool(val)) -> {
              motion.set(id, val)
              nil
            }
            case _ -> {
              motion.set(id, false)
              nil
            }
          }
        }
        case _ -> {}
      }
    }

    Result.Ok(Sensors(motion))
  }
}
