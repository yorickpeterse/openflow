import openflow::http::(Client, Error, Request, Response, retry)
import std::int::ToInt
import std::json
import std::process::(sleep)
import std::time::Duration

# The path to the API endpoint.
let PATH = '/api.html'

# The time after which a request times out, excluding retries.
let TIMEOUT = 60

class pub Status {
  # The CO2 concentration in the plenum.
  let pub @co2: Int

  # The speed of the exhaust fan, as a percentage between 0 and 100.
  let pub @exhaust_speed: Int

  # The humidity levels for every humidity sensor.
  let pub @humidity: Map[String, Int]
}

# An API/HTTP client for interacting with the Itho WiFi add-on API.
class pub Itho {
  let @client: Client

  # The amount of time to wait after retrying a failed operation.
  let pub @retry_wait_time: Duration

  fn pub static new(client: Client) -> Itho {
    Itho { @client = client, @retry_wait_time = Duration.from_secs(5) }
  }

  # Sets a setting.
  #
  # The `setting` argument must be the setting index as obtained from the "Itho
  # settings" page. The `value` argument is the value for said setting. Only
  # integer values are supported.
  fn pub mut set(setting: Int, value: ref ToInt) !! String {
    let request = Request.get(PATH)
    let timeout = Duration.from_secs(TIMEOUT)

    request.query('setsetting', setting.to_string)
    request.query('value', value.to_int.to_string)

    try {
      retry(@retry_wait_time) fn { try execute(request, timeout) }
    } else (err) {
      throw err.to_string
    }
  }

  # Returns the value of a setting.
  #
  # The `setting` argument must be the setting index. See `Itho.set` for more
  # information.
  fn pub mut get(setting: Int) !! String -> Int {
    let request = Request.get(PATH)
    let timeout = Duration.from_secs(TIMEOUT)

    request.query('getsetting', setting.to_string)

    let resp = try {
      retry(@retry_wait_time) fn { try execute(request, timeout) }
    } else (err) {
      throw err.to_string
    }

    let body = resp.body.drain_to_string
    let root = match try json.parse(body) else (e) throw e.to_string {
      case Object(map) -> map
      case _ -> throw 'Expected the status to be a JSON object'
    }

    match root.remove('current') {
      case Some(Int(v)) -> v
      case Some(Float(v)) -> v.to_int
      case _ -> 0
    }
  }

  # Returns the status of the Itho device.
  fn pub mut status !! String -> Status {
    let request = Request.get(PATH)
    let timeout = Duration.from_secs(TIMEOUT)

    request.query('get', 'ithostatus')

    let resp = try {
      retry(@retry_wait_time) fn { try execute(request, timeout) }
    } else (err) {
      throw err.to_string
    }

    let body = resp.body.drain_to_string
    let root = match try json.parse(body) else (e) throw e.to_string {
      case Object(map) -> map
      case _ -> throw 'Expected the status to be a JSON object'
    }

    let humidity = Map.new
    let co2 = match root.get('CO2 plenum (ppm)') {
      case Some(Int(v)) -> v
      case Some(Float(v)) -> v.to_int
      case _ -> 0
    }

    let exhaust_speed = match root.get('exhaust fan (%)') {
      case Some(Int(v)) -> v
      case Some(Float(v)) -> v.to_int
      case _ -> 0
    }

    root.iter.each fn (pair) {
      if pair.key.starts_with?('RH ').false? { return }

      let value = match pair.value {
        case Int(v) -> v
        case Float(v) -> v.to_int
        case _ -> return
      }

      # Sensors that aren't connected report a value that's very close to zero,
      # which due to the conversion to an Int is turned into zero.
      if value > 0 { humidity[pair.key] = value }
    }

    Status { @co2 = co2, @exhaust_speed = exhaust_speed, @humidity = humidity }
  }

  fn mut execute(
    request: ref Request,
    timeout: ref Duration
  ) !! Error -> Response {
    client.timeout_after = timeout

    let response = try client.execute(request)

    # The API may produce an HTTP 500 if an I2C command fails. In this case we
    # want to retry the operation.
    if response.status == 500 {
      throw Error.InternalServerError
    } else {
      response
    }
  }
}
