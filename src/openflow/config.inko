import std::fs::file::ReadOnlyFile
import std::fs::path::IntoPath
import std::json::(self, Json)
import std::net::ip::IpAddress
import std::time::(DateTime, Duration)

fn array(object: mut Map[String, Json], key: String) !! String -> Array[Json] {
  match object.remove(key) {
    case Some(Array(v)) -> v
    case _ -> throw "The '{key}' value must be an array"
  }
}

fn int(object: mut Map[String, Json], key: String) !! String -> Int {
  match object.remove(key) {
    case Some(Int(v)) -> v
    case _ -> throw "The '{key}' value must be an integer"
  }
}

fn string(object: mut Map[String, Json], key: String) !! String -> String {
  match object.remove(key) {
    case Some(String(v)) -> v
    case _ -> throw "The '{key}' value must be a string"
  }
}

fn ip(object: mut Map[String, Json], key: String) !! String -> uni IpAddress {
  match object.remove(key) {
    case Some(String(v)) -> recover match IpAddress.parse(v) {
      case Some(addr) -> return addr
      case _ -> {}
    }
    case _ -> {}
  }

  throw "The '{key}' value must be a valid IP address"
}

# Configuration details for a fixed ventilation schedule.
class pub enum Schedule {
  # Ventilation should be enabled between 23:00 and 11:00.
  #
  # This schedule is meant for bedrooms that are used at night, as these pretty
  # much always need ventilation when in use.
  case Night

  # Ventilation should be enabled between 07:00 and 08:00.
  #
  # This schedule is meant for rooms that may build up stale/smelly air over
  # night.
  case Morning

  # Returns `true` if the schedule is/should be active.
  fn pub active?(time: ref DateTime) -> Bool {
    match self {
      case Night -> time.hour == 23 or time.hour < 11
      case Morning -> time.hour == 7
    }
  }
}

# Configuration details of a single room, such as the name and the motion
# sensor.
class pub Room {
  # The human-friendly name of the room.
  let pub @name: String

  # The setting index used to control the exhaust in a room.
  let pub @exhaust: Int

  # A ventilation schedule to apply.
  let pub @schedule: Option[Schedule]

  # The ID of the motion sensor attached to this room.
  let pub @motion: Option[Int]

  # The ID of the temperature sensor attached to this room.
  let pub @temperature: Option[Int]

  # The name of the Itho humidity sensor attached to this room.
  let pub @humidity: Option[String]

  # Any IPs to ping to determine if a room should be ventilated.
  let pub @ping: Array[IpAddress]

  # The desired flow rate in h3/hour.
  let pub @flow: Int

  fn pub static from_json(value: Map[String, Json]) !! String -> Room {
    let name = try string(value, 'name')
    let exhaust = try int(value, 'exhaust')
    let schedule = match value.remove('schedule') {
      case Some(String('night')) -> Option.Some(Schedule.Night)
      case Some(String('morning')) -> Option.Some(Schedule.Morning)
      case _ -> Option.None
    }

    let motion = match value.remove('motion') {
      case Some(Int(val)) -> Option.Some(val)
      case _ -> Option.None
    }

    let temperature = match value.remove('temperature') {
      case Some(Int(val)) -> Option.Some(val)
      case _ -> Option.None
    }

    let humidity = match value.remove('humidity') {
      case Some(String(val)) -> Option.Some(val)
      case _ -> Option.None
    }

    let flow = match value.remove('flow') {
      case Some(Int(val)) -> val
      case _ -> 15
    }

    let ping = match value.remove('ping') {
      case Some(Array(vals)) -> {
        vals
          .into_iter
          .select_map fn (val) {
            match val {
              case String(val) -> IpAddress.parse(val)
              case _ -> Option.None
            }
          }
          .to_array
      }
      case _ -> []
    }

    Room {
      @name = name,
      @schedule = schedule,
      @exhaust = exhaust,
      @motion = motion,
      @temperature = temperature,
      @humidity = humidity
      @ping = ping,
      @flow = flow,
    }
  }
}

# Configuration details for the humidity sensors.
class pub Humidity {
  # The high threshold.
  #
  # When humidity levels exceed this value, the room is deemed too humid.
  let pub @high: Int

  # The low threshold.
  #
  # When the humidity levels are reduced to or below this level, the room is
  # considered dry enough.
  let pub @low: Int

  # The amount of time to wait after the high threshold is exceeded, before
  # ventilating a room.
  let pub @high_time: Duration

  # The amount of time to wait after the low threshold is reached, before
  # turning ventilation back to normal.
  let pub @low_time: Duration

  # The air flow rate in m3/hour to apply to a room when it's too humid.
  let pub @flow: Int

  fn pub static from_json(value: Map[String, Json]) !! String -> Humidity {
    let high = try int(value, 'high')
    let low = try int(value, 'low')
    let low_time = Duration.from_secs(try int(value, 'low_time'))
    let high_time = Duration.from_secs(try int(value, 'high_time'))
    let flow = try int(value, 'flow')

    Humidity {
      @high = high,
      @low = low,
      @low_time = low_time,
      @high_time = high_time,
      @flow = flow,
    }
  }
}

# Configuration details for the exhaust fan and valves.
class pub Exhaust {
  # The minimum flow of air in m3/hour.
  let pub @min_flow: Int

  # The maximum supported flow of air in m3/hour.
  let pub @max_flow: Int

  # The amount of time to wait for an exhaust valve to close.
  let pub @close_time: Duration

  # The amount of time to wait after an exhaust speed change before we allow
  # reducing the speed again.
  let pub @reduce_wait_time: Duration

  # The setting that controls the speed.
  let pub @speed_setting: Int

  # The setting that enables/disables manual control.
  let pub @manual_setting: Int

  fn pub static from_json(value: Map[String, Json]) !! String -> Exhaust {
    Exhaust {
      @min_flow = try int(value, 'min_flow'),
      @max_flow = try int(value, 'max_flow'),
      @close_time = Duration.from_secs(try int(value, 'close_time')),
      @speed_setting = try int(value, 'speed_setting'),
      @manual_setting = try int(value, 'manual_setting'),
      @reduce_wait_time = Duration.from_secs(try int(value, 'reduce_wait_time')),
    }
  }
}

# Various configuration settings, such as the settings to control the
# ventilation and the room-specific settings.
class pub Config {
  # Rooms to ventilate.
  let pub @rooms: Array[Room]

  # The IP address of the Ith WiFi add-on API.
  let pub @itho_wifi_address: uni IpAddress

  # The IP address of the Philips Hue bridge API.
  let pub @hue_address: uni IpAddress

  # The username/key for the Philips Hue API.
  let pub @hue_user: String

  # The humidity sensor settings.
  let pub @humidity: Humidity

  # The desired CO2 level in parts-per-million.
  let pub @co2: Int

  # The settings to control the exhaust fan and valves.
  let pub @exhaust: Exhaust

  # The amount of time (in seconds) to keep ventilating a room after the last
  # motion event.
  let pub @ventilation_time: Duration

  # The amount of time (in seconds) to wait for motion events.
  let pub @motion_wait_time: Duration

  # The amount of individual minutes motion must be registered in before
  # enabling ventilation.
  let pub @motion_minutes: Int

  fn pub static load(path: IntoPath) !! String -> Config {
    let file = try {
      ReadOnlyFile.new(path)
    } else (err) {
      throw "Failed to load the configuration file: {err}"
    }

    let bytes = ByteArray.new

    try file.read_all(bytes) else (e) throw e.to_string

    let doc = try {
      json.parse(bytes.into_string)
    } else (err) {
      throw "Failed to parse the configuration file: {err}"
    }

    let root = match doc {
      case Object(v) -> v
      case _ -> throw 'The top-level JSON value must be an object'
    }

    let rooms = []
    let rooms_iter = try { array(root, 'rooms') }.into_iter

    loop {
      match rooms_iter.next {
        case Some(Object(map)) -> rooms.push(try Room.from_json(map))
        case Some(_) -> throw "The 'rooms' setting can only contain objects"
        case None -> break
      }
    }

    let humidity = match root.remove('humidity') {
      case Some(Object(map)) -> try Humidity.from_json(map),
      case _ -> throw 'The "humidity" object is required'
    }

    let exhaust = match root.remove('exhaust') {
      case Some(Object(map)) -> try Exhaust.from_json(map)
      case _ -> throw 'The "exhaust" object is required'
    }

    let ventilation_time =
      Duration.from_secs(try int(root, 'ventilation_time'))

    let motion_wait_time =
      Duration.from_secs(try int(root, 'motion_wait_time'))

    Config {
      @rooms = rooms,
      @itho_wifi_address = try ip(root, 'itho_api'),
      @hue_address = try ip(root, 'hue_api'),
      @hue_user = try string(root, 'hue_user'),
      @humidity = humidity,
      @exhaust = exhaust,
      @co2 = try int(root, 'co2'),
      @ventilation_time = ventilation_time,
      @motion_wait_time = motion_wait_time,
      @motion_minutes = try int(root, 'motion_minutes'),
    }
  }
}
