import openflow::clock::SystemClock
import openflow::config::Config
import openflow::controller::Controller
import openflow::http::Client
import openflow::hue::Hue
import openflow::itho::Itho
import openflow::logger::Logger
import std::env
import std::process::(sleep)
import std::time::Duration

# The amount of time (in seconds) to wait between iterations of the main work
# loop.
let INTERVAL = 5

class async Main {
  fn async main {
    let logger = Logger.default
    let conf_path = env.arguments.get(0).unwrap_or('/etc/openflow.json')

    logger.info("Using configuration file {conf_path}")

    let conf = try! Config.load(conf_path)
    let itho = Itho.new(Client.new(conf.itho_wifi_address.clone))
    let hue = Hue.new(Client.new(conf.hue_address.clone), user: conf.hue_user)
    let clock = SystemClock.new
    let ctl = Controller.new(conf, itho, hue, clock, logger)
    let interval = Duration.from_secs(INTERVAL)

    ctl.prepare

    loop {
      ctl.iteration
      sleep(interval)
    }
  }
}
